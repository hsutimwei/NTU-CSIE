// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    And(a=instruction[15], b=instruction[15], out=C);//確認C還是A
    Not(in=C, out=A);
    And(a=C, b=instruction[3], out=writeM);
    //如果是C
    And(a=C, b=instruction[5], out=checkA);// 有沒有要寫到A
    Mux16(a=instruction, b=ALUout, sel=checkA, out=Adata);//確認要寫到A的資料
        //如果是A指令或C指令有寫到A則load
    Or(a=A, b=checkA, out=loadA);    
    ARegister(in=Adata, load=loadA, out=outA);
    And(a=C, b=instruction[4], out=loadD); //有沒有要寫到D
    DRegister(in=ALUout, load=loadD, out=outD);
    
    Mux16(a=outA, b=inM, sel=instruction[12], out=AorM);   // 確認運算是A還是M

    ALU(x=outD, y=AorM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7],no=instruction[6], out=ALUout, out=outM, zr=zr, ng=ng);
    And16(a=outA, b=outA, out[0..14]=addressM);
    
    Or(a=zr, b=ng, out=zrorng);
    Not(in=zrorng, out=out1);

    And(a=out1, b=instruction[0], out=jgt);
    And(a=zr, b=instruction[1], out=jeq);
    And(a=ng, b=instruction[2], out=jlt);

    Or(a=jeq, b=jlt, out=jle);
    Or(a=jle, b=jgt, out=toA);
    And(a=C, b=toA, out=jump);
    Not(in=jump, out=pc1);
    PC(in=outA, load=jump, inc=pc1, reset=reset, out[0..14]=pc);
    
}